apply plugin: 'maven'

def isReleaseVersion() {
    return !version.endsWith("SNAPSHOT")
}

def isSigningRequired() {
    return isReleaseVersion()
}

def getMavenSigningPropertiesFilename() {
    return "$rootDir/maven-signing.properties"
}

def getMavenSigningConfigProperty() {
    return "maven-signing-config"
}

def getSonatypeOssrhPropertiesFilename() {
    return "$rootDir/sonatype-ossrh.properties"
}

def getSonatypeOssrhConfigProperty() {
    return "sonatype-ossrh-config"
}

def getLinkedProperties(String linkedFilename, String propertyName) {
    Properties returnProperties = null
    File linkFile = new File(linkedFilename)
    if (linkFile.exists()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(linkFile))
        if (properties.containsKey(propertyName)) {
            File propertiesFile = new File(properties[propertyName])
            if (propertiesFile.exists()) {
                properties = new Properties()
                properties.load(new FileInputStream(propertiesFile))
                returnProperties = properties
            }
        }
    }
    return returnProperties
}

def getSigningProperties() {
    return getLinkedProperties(getMavenSigningPropertiesFilename(), getMavenSigningConfigProperty())
}

def getSonatypeOssrhProperties() {
    return getLinkedProperties(getSonatypeOssrhPropertiesFilename(), getSonatypeOssrhConfigProperty())
}

// GRADLE-1285 - workaround to get <packaging>jar</packaging>
//               into the pom file
def pomPackagingElement = { root ->
    if (POM_PACKAGING.equalsIgnoreCase("jar")) {
        def children = root.asNode().children()
        def versionIndex = children.indexOf(children.find {it.name().localPart == 'version'})
        children.add(versionIndex + 1, new Node(null, 'packaging', POM_PACKAGING))
    }
}

def getRepositoryUrl(Boolean isSnapshot) {
    String url = null
    if (isSnapshot) {
        url = (ext["useSonatypeOssrhCredentials"]) ? ext["ossrhSnapshotUrl"] : SNAPSHOT_REPOSITORY_URL
    }
    else {
        url = (ext["useSonatypeOssrhCredentials"]) ? ext["ossrhReleaseUrl"] : RELEASE_REPOSITORY_URL
    }
    return url
}

def getRepositoryUsername(Boolean isSnapshot) {
    if (ext["useSonatypeOssrhCredentials"]) {
        return ext["ossrhUsername"]
    }
    else {
        return (isSnapshot) ? SNAPSHOT_REPOSITORY_USERNAME : RELEASE_REPOSITORY_USERNAME
    }
}

def getRepositoryPassword(Boolean isSnapshot) {
    if (ext["useSonatypeOssrhCredentials"]) {
        return ext["ossrhPassword"]
    }
    else {
        return (isSnapshot) ? SNAPSHOT_REPOSITORY_PASSWORD : RELEASE_REPOSITORY_PASSWORD
    }
}

afterEvaluate { project ->
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: project.getRepositoryUrl(false)) {
                authentication(
                    userName: project.getRepositoryUsername(false),
                    password: project.getRepositoryPassword(false))
            }

            snapshotRepository(url: project.getRepositoryUrl(true)) {
                authentication(
                    userName: project.getRepositoryUsername(true),
                    password: project.getRepositoryPassword(true))
            }


        }
    }
}
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = GROUP
            pom.artifactId = POM_ARTIFACT_ID
            pom.version = VERSION_NAME

            pom {
                project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL
                    inceptionYear POM_INCEPTION_YEAR

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                            email POM_DEVELOPER_EMAIL
                        }
                    }

                    issueManagement {
                        system POM_ISSUE_MANAGEMENT_SYSTEM
                        url POM_ISSUE_MANAGEMENT_URL
                    }
                }
            }

            pom.withXml pomPackagingElement
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = 'Builds a javadoc jar artifact suitable for maven deployment'
    classifier = 'javadoc'
    from javadoc
}

task allSourcesJar(type: Jar) {
    description = 'Builds a source jar artifact suitable for maven deployment'
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives allSourcesJar
    archives javadocJar
}

// Disable OSSRH Sonatype Credentials by default
// unless we find the configuration
ext["useSonatypeOssrhCredentials"] = false

// Maven Signing stuff
Properties signingProperties = null
if (isSigningRequired()) {
    signingProperties = getSigningProperties()
}
if (signingProperties != null) {
    // Enable Signing
    apply plugin: 'signing'

    ext."signing.keyId" = signingProperties['keyId']
    ext."signing.password" = signingProperties['password']
    ext."signing.secretKeyRingFile" = signingProperties['secretKeyRingFile']


    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }
        }
    }
    signing {
        sign configurations.archives
        required true
    }
    println "Maven Signing Enabled"

    Properties sonatypeOssrhProperties = getSonatypeOssrhProperties()
    if (sonatypeOssrhProperties != null) {
        ext."ossrhUsername" = sonatypeOssrhProperties['ossrhUsername']
        ext."ossrhPassword" = sonatypeOssrhProperties['ossrhPassword']
        ext."ossrhReleaseUrl" = sonatypeOssrhProperties['ossrhReleaseUrl']
        ext."ossrhSnapshotUrl" = sonatypeOssrhProperties['ossrhSnapshotUrl']
        if (ext["ossrhUsername"] != null && ext["ossrhPassword"] != null &&
            ext["ossrhReleaseUrl"] != null && ext["ossrhSnapshotUrl"] != null) {
            ext["useSonatypeOssrhCredentials"] = true
        }
        println "Using Sonatype OSSRH Credentials"
    }
}
else {
    // Disable Signing
    println "Maven Signing Disabled"
}
