apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'maven'

description = 'Custom Colorizer library for logback'
version = VERSION_NAME

dependencies {
    compile fileTree(dir: 'src', include: ['*.jar'])
    compile 'ch.qos.logback:logback-classic:1.1.2'
    testCompile "org.mockito:mockito-core:1.10.5"
    testCompile "junit:junit:4.11"
    testCompile "junit-addons:junit-addons:1.4"
}

jar {
    manifest {
        attributes 'Implementation-Title' : POM_NAME,
                'Implementation-Version' : VERSION_NAME
    }
}

// Configure FindBugs for maximum effort and to report all failures
findbugs {
    effort = 'max'
    reportLevel = "low"
    ignoreFailures = true
    excludeFilter = file("$projectDir/config/findbugs/excludeFilter.xml")
}
findbugsTest {
    effort = 'max'
    reportLevel = "low"
    ignoreFailures = true
    excludeFilter = file("$projectDir/config/findbugs/excludeFilter.xml")
}

// Enable HTML output from FindBugs instead of XML
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    ruleSets = [
            'java-android',
            'java-basic',
            'java-braces',
            'java-clone',
            'java-codesize',
            'java-comments',
            'java-controversial',
            'java-coupling',
            'java-design',
            'java-empty',
            'java-finalizers',
            'java-imports',
            'java-j2ee',
            'java-javabeans',
            'java-junit',
            'java-logging-jakarta-commons',
            'java-logging-java',
            'java-migrating',
            'java-naming',
            'java-optimizations',
            'java-strictexception',
            'java-strings',
            'java-sunsecure',
            'java-typeresolution',
            'java-unnecessary',
            'java-unusedcode'
            ]
    ignoreFailures = true
    toolVersion = '5.1.3'
}

checkstyle {
    ignoreFailures = true
}

checkstyleMain {
    doLast {
        ant.xslt(in: "$buildDir/reports/checkstyle/main.xml",
                style:"config/checkstyle/checkstyle.xsl",
                out:"$buildDir/reports/checkstyle/checkstyle.html"
        )
        println("See final Checktyle report at $buildDir/reports/checkstyle/checkstyle.html")
    }
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                pom.groupId = GROUP
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = VERSION_NAME

                repository(url: RELEASE_REPOSITORY_URL) {
                    authentication(userName: "", password: "")
                }

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                        }
                    }
                }
            }
        }
    }

    /* Enable later
    signing {
        required gradle.taskGraph.hasTask("uploadArchives")
        sign configurations.archives
    }*/

    task sourceJavadocs(type: Javadoc) {
        source = sourceSets.main.java.srcDirs
        classpath = configurations.compile
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    task allSourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives allSourcesJar
        archives javadocJar
    }
}
